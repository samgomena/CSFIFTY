      ******************************************************************
      * Author: Sam Gomena
      * Date: 5/10/2021
      * Purpose: Assignment 3
      * Tectonics: cobc
      ******************************************************************

       IDENTIFICATION DIVISION.
       PROGRAM-ID. ASSIGNMENT-3.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT COURSES-ALL-UNSORTED ASSIGN TO "COURSES-ALL.TXT"
               ORGANIZATION IS LINE SEQUENTIAL.

           SELECT COURSES-ALL-SORTED ASSIGN TO "COURSES-ALL-SORTED.TXT"
               ORGANIZATION IS LINE SEQUENTIAL.
           
           SELECT SORT-FILE ASSIGN TO "WORK.TMP".
           
           SELECT REPORT-FILE ASSIGN TO "REPORT-ALL.TXT".
       DATA DIVISION.
       FILE SECTION.
       FD COURSES-ALL-UNSORTED.
           01 UNSORTED-RECORD PIC X(229).
       
       FD COURSES-ALL-SORTED.
       01 COURSES-RECORD.
           03 FILLER PIC X(16).
           03 COLLEGE-NAME PIC X(23).
           03 FILLER PIC X(14).
           03 DEPARTMENT-NAME PIC X(25).
           03 FILLER PIC X(76).
           03 SCH PIC X(9).
           03 INSTRUCTOR-NAME PIC X(23).
           03 FILLER PIC X(43).

       SD SORT-FILE.
       01 COURSES-RECORD-SORT.
           03 FILLER PIC X(16).
           03 COLLEGE-NAME-SORT PIC X(23).
           03 FILLER        PIC X(14).
           03 DEPARTMENT-NAME-SORT PIC X(25).
           03 FILLER PIC X(76).
           03 SCH-SORT PIC X(9).
           03 INSTRUCTOR-NAME-SORT PIC X(23).
           03 FILLER PIC X(43).

       FD REPORT-FILE
           REPORT IS COURSES-REPORT.
       WORKING-STORAGE SECTION.
       77 FILE-STATUS       PIC 99 VALUE 0.
           88 EndOfFile     VALUE 1.
       REPORT SECTION.
       RD COURSES-REPORT
           CONTROLS ARE COLLEGE-NAME, DEPARTMENT-NAME, INSTRUCTOR-NAME
           PAGE LIMIT IS 50 LINES.

       01 DEPARTMENT-HEADING TYPE IS CONTROL HEADING COLLEGE-NAME.
           02 FIRST-LINE LINE PLUS 1.
               05 COLUMN 26 PIC X(4) VALUE "*** ".
               05 COLUMN 30 PIC X(23) SOURCE COLLEGE-NAME.
               05 COLUMN 55 PIC X(4) VALUE  " ***".
           02 SECOND-LINE LINE PLUS 1.
               05 COLUMN 30 PIC X(29) VALUE "-----------------------".

       01 REPORT-LINE TYPE IS DETAIL LINE PLUS 1.
               05 COLUMN 4 PIC X(23)  SOURCE INSTRUCTOR-NAME.
               05 COLUMN 40 PIC X(9) SOURCE SCH.

       01 INSTRUCTOR-SUMMARY TYPE IS CONTROL FOOTING INSTRUCTOR-NAME.
           02 LINE PLUS 1.
               05 COLUMN 1 PIC X(23) SOURCE INSTRUCTOR-NAME.
               05 COLUMN 40 PIC ZZ,ZZ9 SUM SCH.

       01 DEPARTMENT-SUMMARY TYPE IS CONTROL FOOTING DEPARTMENT-NAME.
           02 LINE PLUS 1.
               05 COLUMN 7 PIC X(20) VALUE "*DEPARTMENT:".
               05 COLUMN 30 PIC X(25) SOURCE DEPARTMENT-NAME.
               05 COLUMN 60 PIC ZZ,ZZ9 SUM SCH.

       01 COLLEGE-SUMMARY TYPE IS CONTROL FOOTING COLLEGE-NAME
               NEXT GROUP PLUS 2.
           02 LINE PLUS 1.
               05 COLUMN 30 PIC X(10) VALUE IS "**COLLEGE:".
               05 COLUMN 50 PIC X(25) SOURCE COLLEGE-NAME.
               05 COLUMN 80 PIC ZZZ,ZZ9 SUM SCH.

       PROCEDURE DIVISION.
       MAIN-PROCEDURE.
           PERFORM INIT.
           OPEN INPUT COURSES-ALL-SORTED.
           OPEN OUTPUT REPORT-FILE.
           INITIATE COURSES-REPORT.
           READ COURSES-ALL-SORTED AT END MOVE 1 TO FILE-STATUS.
           PERFORM GENERATE-REPORT UNTIL EndOfFile.
           TERMINATE COURSES-REPORT.
           CLOSE COURSES-ALL-SORTED.
           CLOSE REPORT-FILE.
           STOP RUN.

       INIT.
           SORT SORT-FILE ON 
               ASCENDING KEY COLLEGE-NAME-SORT
               ASCENDING KEY DEPARTMENT-NAME-SORT
               ASCENDING KEY INSTRUCTOR-NAME-SORT
               USING COURSES-ALL-UNSORTED
               GIVING COURSES-ALL-SORTED.

       GENERATE-REPORT.
           GENERATE COURSES-REPORT.
           READ COURSES-ALL-SORTED AT END MOVE 1 TO FILE-STATUS.
           
       END PROGRAM ASSIGNMENT-3.
